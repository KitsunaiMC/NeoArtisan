plugins {
    id 'java'
    id("com.gradleup.shadow") version "9.0.0-beta9"
    id 'io.papermc.paperweight.userdev' version '2.0.0-beta.16'
}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    implementation(project(":api"))
    paperweight.paperDevBundle("1.21.4-R0.1-SNAPSHOT")
    implementation 'org.reflections:reflections:0.10.2'
}

paperweight {
    reobfJar.enabled = true
}

build {
    dependsOn(shadowJar)
}

shadowJar {
    minimize()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

tasks.withType(Javadoc).configureEach {
    options.encoding = "UTF-8"
    options.addStringOption('Xdoclint:none', '-quiet')
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
